import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'net.corda.cordapp.cordapp-configuration'
    id 'org.jetbrains.kotlin.plugin.jpa'
    id 'java'
    id 'maven-publish'
    id 'csde'
}

allprojects {
    group 'net.corda.samples'
    version '1.0-SNAPSHOT'

    def javaVersion = VERSION_11

    // Declare the set of Kotlin compiler options we need to build a CorDapp.
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            allWarningsAsErrors = false

            // Specify the version of Kotlin that we are that we will be developing.
            languageVersion = '1.7'
            // Specify the Kotlin libraries that code is compatible with
            apiVersion = '1.7'
            // Note that we Need to use a version of Kotlin that will be compatible with the Corda API.
            // Currently that is developed in Kotlin 1.7 so picking the same version ensures compatibility with that.

            // Specify the version of Java to target.
            jvmTarget = javaVersion

            // Needed for reflection to work correctly.
            javaParameters = true

            // -Xjvm-default determines how Kotlin supports default methods.
            // JetBrains currently recommends developers use -Xjvm-default=all
            // https://kotlinlang.org/api/latest/jvm/stdlib/kotlin.jvm/-jvm-default/
            freeCompilerArgs += [
                    "-Xjvm-default=all"
            ]
        }
    }

    repositories {
        // All dependencies are held in Maven Central
        mavenCentral()
        mavenLocal()
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

}

publishing {
    publications {
            maven(MavenPublication) {
                artifactId "corda5-obligation-cordapp"
                groupId project.group
                artifact jar
         }
    }
}

clean.doFirst {
    delete "${rootDir}/workspace/"
    delete "${rootDir}/logs/"
}